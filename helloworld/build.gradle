import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.20'
    id 'idea'
    id 'java-library'
    id 'maven-publish'
}

group = "io.opencui.extensions"
version = '1.0.1-SNAPSHOT'

ext {
    kotlin_version = '2.1.20'
    jackson_version = '2.17.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

kotlin {
    jvmToolchain(21)
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
    }
}

repositories {
    maven { url 'https://repository.jboss.org/nexus/content/groups/public/' }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'io.opencui:core'
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jackson_version}"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${kotlin_version}"
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        def repo_dir = "${layout.buildDirectory.get()}/repos/"
        maven {
            url = uri(version.endsWith('SNAPSHOT') ?
                "${repo_dir}/snapshots" : "${repo_dir}/releases")
        }
    }
}